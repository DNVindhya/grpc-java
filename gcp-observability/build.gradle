plugins {
    id "java-library"
    id "maven-publish"

    id "com.google.protobuf"
    id "ru.vyarus.animalsniffer"
}

description = "gRPC: Google Cloud Platform Observability"

[compileJava].each() {
    it.options.compilerArgs += [
            // only has AutoValue annotation processor
            "-Xlint:-processing"
    ]
    appendToProperty(
            it.options.errorprone.excludedPaths,
            ".*/build/generated/sources/annotationProcessor/java/.*",
            "|")
}

dependencies {
    def cloudLoggingVersion = '3.7.5'
    def opencensusVersion = '0.31.0'

    annotationProcessor libraries.autovalue
    api project(':grpc-api')

    implementation project(':grpc-protobuf'),
            project(':grpc-stub'),
            project(':grpc-alts'),
            libraries.autovalue_annotation,
            libraries.perfmark,
            ('com.google.guava:guava:31.1-jre'),
            ('com.google.errorprone:error_prone_annotations:2.13.0'),
            ('org.checkerframework:checker-qual:3.21.4'),
            ('com.google.http-client:google-http-client:1.41.7'),
            ('com.google.http-client:google-http-client-gson:1.41.7'),
            ('com.google.auth:google-auth-library-oauth2-http:1.6.0'),
            ('com.google.api.grpc:proto-google-common-protos:2.8.3'),
            ('com.google.protobuf:protobuf-java:3.19.4'),
            ('com.google.code.gson:gson:2.9.0'),
            ("com.google.cloud:google-cloud-logging:${cloudLoggingVersion}")

    implementation project(':grpc-census'),
            libraries.opencensus_contrib_grpc_metrics,
            ("io.opencensus:opencensus-exporter-stats-stackdriver:${opencensusVersion}"),
            ("io.opencensus:opencensus-exporter-trace-stackdriver:${opencensusVersion}")

    runtimeOnly libraries.opencensus_impl

    testImplementation project(':grpc-testing'),
            project(':grpc-testing-proto'),
            project(':grpc-netty-shaded')

    testImplementation (libraries.guava_testlib) {
        exclude group: 'junit', module: 'junit'
    }

    signature "org.codehaus.mojo.signature:java18:1.0@signature"
}

configureProtoCompilation()
